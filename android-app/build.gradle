import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}



def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
catch(Exception e) {
}

ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = project.hasProperty('versionCode') ? project.property('versionCode') as int : 1

def versionCode() {
    if (!project.hasProperty('versionCode'))
        return 1
    def code = versionMajor * 1000000 + versionMinor * 1000 + versionPatch
    println "versionCode is set to $code"
    return code
}

def versionName() {
    if (!project.hasProperty('versionCode'))
        return "1.0"
    def name = "${versionMajor}.${versionMinor}.${versionPatch}"
    println "versionName is set to $name"
    return name
}


android {
    compileSdkVersion AndroidSdk.compile

    signingConfigs {

        debug {
            keyAlias 'debug'
            keyPassword 'android'
            storeFile file('../debug.jks')
            storePassword 'android'
        }

        config {
            storeFile file('/Users/joreilly/dev/keystore/galwaybus_android.jks')
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            v2SigningEnabled true
        }
    }

    defaultConfig {
        applicationId "dev.johnoreilly.galwaybus"
        minSdkVersion AndroidSdk.min
        targetSdkVersion AndroidSdk.target

        versionCode this.versionCode()
        versionName this.versionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        final String googleMapsKey = System.getenv('GOOGLE_API_KEY') ?: 'test'
        resValue 'string', 'google_maps_key', googleMapsKey
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check",
                   "-Xuse-experimental=kotlin.time.ExperimentalTime"]

    }
}

dependencies {
    add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, "androidx.compose.compiler:compiler:${Versions.compose}")
    implementation "androidx.compose.runtime:runtime:${Versions.compose}"

    implementation("androidx.appcompat:appcompat:1.3.0-rc01")
    implementation("com.google.android.material:material:1.3.0")
    implementation("androidx.lifecycle:lifecycle-extensions:2.2.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1")
    implementation "androidx.datastore:datastore-preferences:1.0.0-rc01"
    implementation platform('com.google.firebase:firebase-bom:26.2.0')
    implementation("com.google.firebase:firebase-analytics-ktx")
    implementation("androidx.activity:activity-compose:1.3.0-rc01")

    implementation(Koin.core)
    implementation(Koin.android)

    implementation("com.google.android.libraries.maps:maps:3.1.0-beta")
    implementation("com.google.maps.android:maps-v3-ktx:3.0.1")

    implementation(Compose.ui)
    implementation(Compose.uiGraphics)
    implementation(Compose.uiTooling)
    implementation(Compose.foundationLayout)
    implementation(Compose.material)
    implementation(Compose.navigation)
    implementation(Compose.accompanist)

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.1'
    implementation PlayServices.location

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation project(":SharedCode")
}

